# Teleporter

Esta tecnología nos permite comunicarnos entre una Blockchain y otra, en este caso entre una subnet y otra, por ahora está disponible entre las subnets de Dispatch (D), Echo (E) y C-Chain; si queremos usar estas redes es muy importante que tengamos los tokens nativos, los cuales los podemos solicitar del faucet oficial de Avalanche haciendo clic <a href="https://core.app/tools/testnet-faucet/?subnet=dispatch&token=dispatch" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">aquí para Dispatch</a>, <a href="https://core.app/tools/testnet-faucet/?subnet=echo&token=echo" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">aquí para Echo</a>, y <a href="https://core.app/tools/testnet-faucet/?subnet=c&token=c" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">aquí para C-Chain</a>; adicional a los token nativos, es importante que tengamos Tokens de TP, los cuales son usados para poder usar este servicio, los podemos encontrar en <a href="https://ohmywarp.com/mint" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">https://ohmywarp.com/mint</a>, por el momento solo podemos mintear TP para C-Chain, pero podemos hacer luego un bridge y mandar TP a las otras dos redes.

Una vez tengamos tokens en las respectivas redes podemos plantearnos la pregunta ¿Cómo mandamos información de un contrato en la red de origen a un contrato en la red de destino?

Para responder a esta pregunta debemos entender que significa mandar información de una red a otra, imaginemos lo siguiente

<div style="display:flex; flex-direction:row; justify-content: center; align-items:center;">
    <div style="max-width: 45%;width: 300px; margin: 0.5rem; display:flex; flex-direction:column; justify-content: center; align-items:center;">
        <img
            src="/images/teleporter_1.png"
            alt="Teleporter 1"
            style="width: 100%"
        />
        <div>Red de origen</div>
    </div>
    <div style="max-width: 45%;width: 300px; margin: 0.5rem; display:flex; flex-direction:column; justify-content: center; align-items:center;">
        <img
            src="/images/teleporter_1.png"
            alt="Teleporter 2"
            style="width: 100%"
        />
        <div>Red de destino</div>
    </div>
</div>

Dos Subnet diferentes que están técnicamente aisladas, cada una con información acerca del estado de ellas mismas y sin información del estado de la otra, en este caso debe de haber un ente afuera de las subnets que esté escuchando información de algún lado y procesando dicha información, enviandola de la subnet de origen a la subnet de de destino cuando alguna condición se cumpla.

<div style="display:flex; flex-direction:row; justify-content: center; align-items:center;">
    <img
        src="/images/teleporter_2.png"
        alt="Teleporter 3"
        style="max-width: 45%;width: 300px; margin: 0.5rem; display:flex; flex-direction:column; justify-content: center; align-items:center; "
    />
</div>

Esa entidad afuera de la blockchain se llama <b>Relayer</b> y se encarga de escuchar la información proveniente de un contrato especial en la red de origen, validar la veracidad de este y de los validadores en dicha red y enviar ese mensaje en la red de destino, esto implica algo más complejo a nivel técnico pero <b>Teleporter</b> nos permite simplificarlo a eso, por lo que el usar esta tecnología nos facilita mucho el proceso de mandar información entre dos subnets.

Ahora veamos a nivel técnico que debemos saber para poder mandar estos mensajes y recibirlos de manera correcta.

## Contratos de teleporter

Para empezar debemos entender que en este proceso de enviar y recibir información debemos hacer uso de cuatro contratos que podemos encontrar en repositorio oficial de <a href="https://github.com/ava-labs/teleporter/tree/main/contracts/src/Teleporter" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ava-labs</a>, uno es el contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterMessenger.sol</a>, otro es el contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterReceiver.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterReceiver.sol</a>, ambos son importantes en nuestro proceso de mandar y recibir información entre subnets, ya que usaremos algunas funciones e interfaces de ellos.

Lo otros dos se agregaron recientemente en una actualización llamada durango los cuales son <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">TeleporterRegistry</a> y IWarpMessenger, el cual es referenciado en <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">TeleporterRegistry</a> y son usados para manejar un versionado de los contratos de Teleporter en cada red.

## Implementación de salida de información

Un contrato que enviará información de una subnet de origen a una subnet de destino debe importar el contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterMessenger.sol</a>, el cual le permitirá hacer uso de dicha interfaz, y por ende de sus métodos, entre los cuales hay uno llamado <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">sendCrossChainMessage</span>.

A su vez debe importar el contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;"> TeleporterRegistry</a>, para tener acceso a la versión de Teleporter que usará.

Si pensamos por un momento, estamos accediendo a un contrato y estamos llamando una función de este, lo que implica que debe de existir previamente un contrato desplegado en la subnet que estamos usando de origen de la información, y este debe tener literalmente el código que aparece en <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterMessenger.sol</a>, esta es una de las razones por las cuales limitaremos las pruebas por el momento a las tres subnets mencionadas al principio de esta sección.

### Instancia

Ahora con lo anterior en mente y siendo conscientes de que estamos haciendo, podemos decir que debemos definir una instancia del contrato de TeleporterRegistry en nuestro contrato para poder acceder a sus métodos y a las versiones de TeleporterMessenger, para eso debemos darle la dirección en la cual fue previamente desplegado dicho contrato, en nuestra subnet de origen, este contrato está desplegado en la dirección: <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">0xEeeAA8e0e25802A3748Cd7FbFA96b851E76DFF9b</span> por lo que una instancia se resume a escribir la siguiente linea de código:

<span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
    TeleporterRegistry public immutable teleporterRegistry =
    TeleporterRegistry(0xEeeAA8e0e25802A3748Cd7FbFA96b851E76DFF9b);
</span>

Es decir, una variable publica, inmutable de tipo <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">TeleporterRegistry</span> que viene de la importación del repositorio de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;"> TeleporterRegistry</a>, con el nombre de teleporterRegistry, la cual es igual a una instancia de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">TeleporterRegistry</span> a la dirección <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">0xEeeAA8e0e25802A3748Cd7FbFA96b851E76DFF9b</span>.

Ahora instanciamos nuestro teleporterMessenger así:

<span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
    ITeleporterMessenger teleporterMessenger =
    teleporterRegistry.getLatestTeleporter();
</span>

### Envío de la información

Para enviar la información podemos simplemente llamar la instancia que tenemos en nuestro contrato del contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterMessenger.sol</a>, y llamar el método <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">sendCrossChainMessage</span>, al hacerlo debemos enviar lo siguiente:

```
TeleporterMessageInput({
	destinationBlockchainID: destinationBlockchainID,
  destinationAddress: destinationAddress,
  feeInfo: TeleporterFeeInfo({
	  feeTokenAddress: address(0),
    amount: 0
  }),
  requiredGasLimit: 100000,
  allowedRelayerAddresses: new address[](0),
  message: message
})
```

1. <b>destinationBlockchainID</b> es el identificador de la subnet que recibirá el
   mensaje dentro de la P - Chain de Avalanche, que en el caso de Dispatch es <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
       0x9f3be606497285d0ffbb5ac9ba24aa60346a9b1812479ed66cb329f394a4b1c7
   </span> en el caso de C-Chain es <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
       0x7fc93d85c6d62c5b2ac0b519c87010ea5294012d1e407030d6acd0021cac10d5
   </span> y en el caso de Echo es <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
       0x1278d1be4b987e847be3465940eb5066c4604a7fbd6e086900823597d81af4c1
   </span>

2. <b>destinationAddress</b> es la dirección del contrato que recibirá el mensaje
   en la subnet de destino, es decir la dirección del contrato receptor.
3. <b>feeInfo</b> tiene la información de la dirección del ERC20 y la cantidad que
   se pagará como incentivo al Relayer por su labor, en los casos de prueba de las
   redes mencionadas hay un Relayer público, por lo que no hay necesidad de enviar
   un incentivo a estos, por eso definimos una dirección de 0 y un amount de 0 (Esto
   será muy importante ya que juega un papel clave al ser el incentivo de los entes
   que envian la información entre las subnets).
4. <b>requiredGasLimit</b> es el limite de gas requerido para ejecutar la transacción,
   si no se define el relayer puede enviar la transacción con menos gas y aún así
   recibir la recompensa independientemnte de que se entregue o no el mensaje en
   la subnet de llegada.
5. <b>allowedRelayerAddresses</b> es una lista blanca de direcciones que pueden servir
   de Relayers para mandar información de nuestra red a la red de destino, si está
   vacía como en nuestro ejemplo indica que cualquiera puede tomar la solicitud,
   procesarla y recibir la recompensa, en el caso de dos subnets privadas con información
   sensible, se puede definir un relayer de confianza que pueda retransmitir la información
   sin exponerla fuera de una frontera geografica o de una empresa.{" "}
6. <b>message</b> es el mensaje que se envia en formato bytes, para hacerlo podemos
   hacer uso de la función <b>abi.encode()</b>, un ejemplo al enviar dos
   números, <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
       num1
   </span> y <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
       num2
   </span> es el siguiente
   <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
       bytes memory message = abi.encode(num1,num2);
   </span>

    donde la variable <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
    message</span> se encuentra en el formato adecuado para ser enviado.

### Ejemplo de código

A continuación, veremos un ejemplo de un código que envía información desde la subnet de C-Chain a la subnet de Dispatch

```solidity
// SPDX-License-Identifier: Ecosystem

pragma solidity 0.8.18;

import "https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol";
import "https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol";

contract SenderOnCChainWithRegistry {

    // The Teleporter registry contract manages different Teleporter contract versions.
    TeleporterRegistry public immutable teleporterRegistry;

  	constructor(address teleporterRegistryAddress) {
        require(
            teleporterRegistryAddress != address(0),
            "SenderOnCChain: zero teleporter registry address"
        );

        teleporterRegistry = TeleporterRegistry(teleporterRegistryAddress);
    }

    function sendMessage(
        address destinationAddress,
        string calldata message
    ) external {
        ITeleporterMessenger teleporterMessenger = teleporterRegistry.getLatestTeleporter();

        teleporterMessenger.sendCrossChainMessage(
            TeleporterMessageInput({
                destinationBlockchainID: 0x9f3be606497285d0ffbb5ac9ba24aa60346a9b1812479ed66cb329f394a4b1c7,
                destinationAddress: destinationAddress,
                feeInfo: TeleporterFeeInfo({
                    feeTokenAddress: address(0),
                    amount: 0
                }),
                requiredGasLimit: 100000,
                allowedRelayerAddresses: new address[](0),
                message: abi.encode(message)
            })
        );
    }
}
```

En el anterior código de ejemplo se mandan un texto a un contrato en la subnet de Dispatch, la función <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">sendMessage</span> se encarga de recibir la dirección del contrato en Dispatch y el mensaje que se quiere enviar, con dicha información se emite un evento con la información de la dirección del contrato en Dispatch, el identificador de esa subnet en la P - Chain, una recompensa para el Relayer que tome la transacción, instrucciones de cuanto gas puede gastar, una lista de los Relayers que pueden ejecutarla (En este ejemplo es cero por lo que cualquiera la puede tomar) y el mensaje en bytes.

En ese punto un relayer de manera automática toma la solicitud y hace las validaciones pertinentes enviando el mensaje a la subnet de destino.

## Implementación de recepción de información

Un contrato que recibirá la información en una Subnet de destino proveniente de una subnet de origen, debe importar los contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterMessenger.sol</a>, de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterReceiver.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterReceiver.sol</a> y de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">TeleporterRegistry</a>, al hacerlo podra instanciar el contrato de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">ITeleporterMessenger</span> y validar quien trata de llamar sus métodos, y la función más importante en este caso, la de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">receiveTeleporterMessage</span> , esta última es la ejecutada por el <b>Relayer</b> una vez a confirmado que todo está correcto y que puede enviar el mensaje, esta función le entrega a este contrato en la Subnet de destino el id de la cadena de origen del mensaje en la P - Chain, la dirección del contrato que le envió el mensaje en la cadena de origen y el mensaje que le enviaron.

Si rompemos nuevamente la cuarta pared y tomamos consciencia sin irnos en piloto automático por el mundo, nos daremos cuenta que en esta subnet también debe de haber sido desplegado un contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterMessenger.sol</a> y <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">TeleporterRegistry</a> y casualmente la dirección de este contrato es la misma que en <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">C-Chain</span>: <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">0xEeeAA8e0e25802A3748Cd7FbFA96b851E76DFF9b</span> . Bueno nada es casualidad, tiene que ver con la wallet usada para el despliegue, pero de eso hablaremos en otra ocasión.

### Instancia

Con lo anterior en mente y siendo conscientes nuevamente de que estamos haciendo, podemos decir que debemos definir una instancia de ese contrato en nuestro contrato para poder acceder a sus métodos, y para eso debemos darle la dirección en la cual fue previamente desplegado dicho contrato, en nuestra subnet de destino, este contrato está desplegado en la dirección: <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">0xEeeAA8e0e25802A3748Cd7FbFA96b851E76DFF9b</span> por lo que una instancia se resume a escribir la siguiente linea de código:

<span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
    TeleporterRegistry public immutable teleporterRegistry =
    TeleporterRegistry(0xEeeAA8e0e25802A3748Cd7FbFA96b851E76DFF9b);
</span>

Es decir, una variable publica, inmutable de tipo <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">TeleporterRegistry</span> que viene de la importación del repositorio de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">TeleporterRegistry</a>, con el nombre de teleporterRegistry, la cual es igual a una instancia de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">TeleporterRegistry</span> a la dirección <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">0xEeeAA8e0e25802A3748Cd7FbFA96b851E76DFF9b</span>.

Ahora instanciamos nuestro teleporterMessenger así:

<span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">
    ITeleporterMessenger teleporterMessenger =
    teleporterRegistry.getLatestTeleporter();
</span>

### Recepción de información

Para recibir la información podemos simplemente llamar la función <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">receiveTeleporterMessage</span> que proviene del contrato de <a href="https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterReceiver.sol" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">ITeleporterReceiver.sol</a>, y usar las variables de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">originChainID</span>, <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">originSenderAddress</span> y <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">message</span>

Pensemos de nuevo conscientemente un momento, estamos accediendo a funciones de un código, ¿Cómo es posible este suceso? Esto es gracias a la herencia por lo tanto nuesto contrato debe heredar de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">ITeleporterReceiver,</span> para esto se debe crear el contrato así:

```solidity
contract ReceiveMessageOnDispatch is ITeleporterReceiver {
	...
}
```

Con esto aseguramos que podemos hacer uso de las funciones de ITeleporterReceiver, funciones como la de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">receiveTeleporterMessage.</span>

Una vez tenemos la información y validamos que si venga de un Relayer y de otra blockchain y no la llamo un usuario desde un explorador de bloques o desde otro contrato, es el momento de ver el mensaje, y para eso debemos pasar de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">bytes</span> a nuestra tipo original, en nuestro ejemplo unos <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">uint256</span>, para eso podemos acceder a los variables de la siguiente manera:

```solidity
lastMessage = abi.decode(message, (string));
```

Con la linea anterior estamos diciendole a nuestro código que va a crear una variable local de tipo <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">string</span> y su valor será el obtenido luego de pasar por un proceso de <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">decode</span> del mensaje recibido.

Luego de esto podemos procesar las variables <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">lastMessage</span> como queramos en las funciones que queramos.

### Ejemplo de código

A continuación, veremos un ejemplo de un código de un contrato que está recibiendo un mensaje en Dispatch proveniente de C-Chain

```solidity
// SPDX-License-Identifier: Ecosystem

pragma solidity 0.8.18;

import "https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/upgrades/TeleporterRegistry.sol";
import "https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterMessenger.sol";
import "https://github.com/ava-labs/teleporter/blob/main/contracts/src/Teleporter/ITeleporterReceiver.sol";

contract ReceiverOnDispatchWithRegistry is ITeleporterReceiver {

    // The Teleporter registry contract manages different Teleporter contract versions.
    TeleporterRegistry public immutable teleporterRegistry;
    bytes32 public myOriginChainID;
    address public myOriginSenderAddress;

  	constructor(address teleporterRegistryAddress) {
        require(
            teleporterRegistryAddress != address(0),
            "SenderOnCChain: zero teleporter registry address"
        );

        teleporterRegistry = TeleporterRegistry(teleporterRegistryAddress);
    }

    string public lastMessage;

    function receiveTeleporterMessage(
        bytes32 originChainID,
        address originSenderAddress,
        bytes calldata message
    ) external {
        myOriginChainID = originChainID;
        myOriginSenderAddress = originSenderAddress;
        // Only the Teleporter receiver can deliver a message. Function throws an error if
        // msg.sender is not registered
        teleporterRegistry.getVersionFromAddress(msg.sender);

        // Store the message.
        lastMessage = abi.decode(message, (string));
    }
}
```

En el anterior código se ve como la función <span style="background-color: #f5f5f5; padding: 2px 4px; border-radius: 4px; font-family: monospace;">receiveTeleporterMessage</span> se encarga de recibir el mensaje, validar la procedencia de este y ejecutar lógica con base en lo recibido, dicha lógica modifica el estado de las variables en dicho contrato por lo que es facil validar que si se ejecuto correctamente nuestra conexión entre las subnets.

## Conclusión

Luego de revisar a detalle cada contrato podemos concluir que el envío de información entre subnets haciendo uso de Teleporter requiere de un contrato emisor que instancia un contrato de Teleporter en la subnet de origen y un contrato receptor que hereda un contrato de teleporter e instancia otro en la subnet de destino, a su vez, sabemos que los mensajes se mandan en tipo bytes, por lo que es muy importante hacer el <b>encode</b> y <b>decode</b>, tanto en la red de origen como en la de llegada respectivamente.

Luego de tener la información el cielo es el limite, ya que podemos mandar cualquier tipo de dato y de lógica entre una red y otra, los invito a contribuir en este <a href="https://github.com/alejandro99so/teleporter" target="_blank" rel="noopener noreferrer" style="text-decoration: none; font-weight: bold; color: black;">proyecto</a> creado por mi, donde hay un ejemplo de un contrato de teleporter desacoplando un poco el envio de información y la recepción con contratos proxies, usando hardhat con typescript, allí podrán aumentar la complejidad del ejemplo e iremos construyendo estructuras más interesante para aprender entre todos.
